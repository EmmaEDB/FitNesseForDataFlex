Use cSeqFile.pkg
Use cSystemIO_Path.pkg
Use cDateAndTimeMethods.pkg
Use vWin32fh.pkg

Class cFitNesseRunnerLogFile is a cSeqOutputFile
    Procedure Construct_Object
        Forward Send Construct_Object
        Property Boolean pbLogFileOpen
    End_Procedure
    
    Procedure CreateLogFile
        String sPath sFileName
        Integer iRetVal
        Boolean bOK
        Get GetApplicationPath of ghoApplication to sPath
        Move (RemoveLastPartOfPath(gcSystemIO_Path, sPath)) to sPath
        Move (vFolderFormat(sPath) + "FitnesseLog") to sPath
        If (not(vFolderExists(sPath))) Begin
            Move (vshCreateDirectoryEX(sPath)) to iRetVal
            If (iRetVal = 1) Begin
                Error DFERR_SETUP ("Failed to create folder" * sPath)
                Abort
            End
        End
        Move (vFolderFormat(sPath) + "FitNesseRunner_" + TimeStampNow(gcDateAndTimeMethods) + ".txt") to sFileName
        Move (UniqueFileName(gcSystemIO_Path, sFileName)) to sFileName
        Get OpenOutput sFileName to bOK
        Set pbLogFileOpen to bOK
    End_Procedure
    
    //Todo: This is the wrong abstraction level - move to file handling class
    Procedure Append_Output_Channel Integer iChannel String sFile
        String sFileANSI
        Move (ToANSI(sFile)) to sFileANSI
        Append_Output channel iChannel sFileANSI
    End_Procedure
    
    //Todo: Move to superclass. Remember to include call to SaveFile from WriteLine
    Procedure SaveToFileAndKeepOpen
        String sFile
        Get psCompleteFilename to sFile
        If (pbToANSI(Self)) Move (ToANSI(sFile)) to sFile
        Close_Output channel (piChannel(Self))
        Send Append_Output_Channel (piChannel(Self)) (psFileMode(Self) + sFile)
    End_Procedure
    
    Procedure WriteLine String sLine
        Boolean bOK
        If (not(pbLogFileOpen(Self))) Send CreateLogFile
        Get WriteLine sLine to bOK
        Send SaveToFileAndKeepOpen
    End_Procedure
    Procedure OutputLine String sLine
        Send WriteLine sLine
    End_Procedure
End_Class
